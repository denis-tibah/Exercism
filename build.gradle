subprojects { project ->
  apply plugin: "java"
  apply plugin: "eclipse"
  apply plugin: "idea"

  repositories {
    mavenCentral()
  }

  dependencies {
    testCompile "junit:junit:4.12"
    testCompile 'org.assertj:assertj-core:2.2.0'
  }

  sourceSets {
    // Verify that the tests are working by running them against the example code.
    // By replacing the "main" sourceSet with the example code we avoid any collisions
    //   with solution code that may have been included as a "starter" (e.g. etl).
    main {
      java.srcDirs = ["src/example/java"]
    }
    project["compileJava"].doFirst { compileTask ->
      println "  (source = " + compileTask.source.asPath + ")"
    }

    starterSource {
      java.srcDirs = ["src/main/java"]
    }
    project["compileStarterSourceJava"].doFirst { compileTask ->
      println "  (source = " + compileTask.source.asPath + ")"
    }
  }
}
